buildscript {
    repositories {
		mavenCentral()
		mavenLocal()
		maven {
			name = "sonatype"
			url = "https://oss.sonatype.org/content/repositories/snapshots/"
		}
		maven {
            name = "github"
            url = "https://github.com/juanmuscaria/maven/raw/master"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-1.1.0-SNAPSHOT'
    }
}

apply plugin: "java"
apply plugin: "forge"

sourceCompatibility = targetCompatibility = 1.8
compileJava.options.encoding = compileTestJava.options.encoding = "UTF-8"

version = modVersion
group = modGroup
archivesBaseName = modBaseName

minecraft {
    version = "1.7.10-10.13.4.1614-1.7.10"
	runDir = "eclipse"
}

repositories {
    maven {
        name = 'spongepowered-repo'
        url = 'https://repo.spongepowered.org/maven/'
    }
    mavenCentral()
}

dependencies {
    annotationProcessor("org.spongepowered:mixin:0.7.11-SNAPSHOT")
    compile "org.spongepowered:mixin:0.7.11-SNAPSHOT"
}

sourceSets {
    main {
        ext.refMap = "mixins.sweetmixin.refmap.json"
    }
}

def refMap = "${tasks.compileJava.temporaryDir}" + File.separator + "mixins.sweetmixin.refmap.json"
jar {
    from refMap
    manifest.attributes(
            "TweakClass": "org.spongepowered.asm.launch.MixinTweaker",
            "MixinConfigs": "mixins.sweetmixin.json",
            "FMLCorePluginContainsFMLMod": "true",
            "ForceLoadAsMod": "true",
    )
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"
        expand "version": project.version, "mcversion": project.minecraft.version
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }
}

def mixinSrg = "${tasks.reobf.temporaryDir}" + File.separator + "mixins.srg"
afterEvaluate {
    tasks.compileJava.options.compilerArgs += ["-AreobfSrgFile=${tasks.reobf.srg}", "-AoutSrgFile=${mixinSrg}", "-AoutRefMapFile=${refMap}"]
}

reobf {
    addExtraSrgFile mixinSrg
}
